apply plugin: 'java'

repositories {
    jcenter()
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

archivesBaseName = "epoll"


ext {
    descriptionString = 'Event-based socket server, which uses epoll'

    cWorkDir = 'src/main/c/com/wizzardo/epoll'
    jdk = System.getenv('JAVA6_HOME')
    if (!jdk)
        throw new IllegalArgumentException('please specify JAVA6_HOME')

    linux64 = 'build/libepoll-core_x64.so'
    linux32 = 'build/libepoll-core_x32.so'
}
apply from: 'gradle/sonatype.gradle'


dependencies {
//    compile files(
//            '../Utils/build/libs/tools-all-0.13-SNAPSHOT.jar'
//    )
    compile 'com.wizzardo.tools:tools-io:0.+'

    testCompile 'junit:junit:4.+'
    testCompile 'com.wizzardo.tools:tools:0.+'
}


jar {
    dependsOn 'compileLinux32'
    dependsOn 'compileLinux64'
    from 'build/libepoll-core_x64.so'
    from 'build/libepoll-core_x32.so'
}

task jar32(type: Jar, dependsOn: ['compileLinux32']) {
    baseName = project.name + '-x32'
    from 'build/libepoll-core_x32.so'
    from sourceSets.main.output
}

task jar64(type: Jar, dependsOn: ['compileLinux64']) {
    baseName = project.name + '-x64'
    from 'build/libepoll-core_x64.so'
    from sourceSets.main.output
}

task compileLinux64(type: Exec, dependsOn: ['prepareHeaders']) {
    workingDir cWorkDir
    commandLine 'gcc', '-shared', '-fpic', '-o', new File(linux64).absolutePath, '-I',
            jdk + '/include/', '-I', jdk + '/include/linux/', 'EpollCore.c', '-lssl', '-lcrypto'
}

task compileLinux32(type: Exec, dependsOn: ['prepareHeaders']) {
    workingDir cWorkDir
    commandLine 'gcc', '-shared', '-fpic', '-o', new File(linux32).absolutePath, '-I',
            jdk + '/include/', '-I', jdk + '/include/linux/', 'EpollCore.c', '-m32', '-lssl', '-lcrypto'
}

task prepareHeaders(type: Exec, dependsOn: ['compileJava']) {
    commandLine 'javah', '-classpath', 'build/classes/main', '-jni', '-v', '-d', cWorkDir, 'com.wizzardo.epoll.EpollCore'
}